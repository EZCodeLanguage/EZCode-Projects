# project properties : name:Graphing, isvisual:True

method start
    global var grid 3
    global var space 50
    global var width 2
    global var point_width 12
    global var pad 24
    global var center : math grid * space + pad
    global shape PointOnScreen bg:[220;50;50], x:center, y:center, w:point_width, h:point_width, p:8
    global group Graph new
    CreateLines
    bringto front PointOnScreen
    textbox position t:0\c\_0, bg:[0;0;0], fc:[255;255;255], w:100
    button generate t:Generate Graph, bg:[0;0;0], fc:[255;255;255], y:30, w:100
    textbox graph t:graph\;3\c\_space\;50, bg:[0;0;0], fc:[255;255;255], w:100, x:110
    button screen t:Adjust Graph, bg:[0;0;0], fc:[255;255;255], y:30, w:100, x:110
    bringto front generate
    bringto front position
    bringto front graph
    bringto front screen
    event generate click => {
        list all new | all split position:text ,
        var xpos all:0
        var ypos all:1
        var x : math xpos * space + center
        var y : math ypos * space * -1 + center
        PointOnScreen x:x, y:y
    }
    event screen click => {
        list all new | all split graph:text ,
        list gr new | gr split all:0, : | var gri gr:1
        list sp new | sp split all:1, : | var spi sp:1
        print \"'gri'\", \"'spi'\"
        grid = gri
        space = spi
        Graph destroyall
        global group Graph new
        center : math grid * space + pad
        CreateLines
        PointOnScreen x:center, y:center
        bringto front PointOnScreen
    }
    loop true {
        await 100
    }
endmethod

method CreateLines
    var half_point_width : math point_width / 2
    var spec : math neg(grid)
    // Horizantal
    var index 0
    loop (grid * 2 - 1) {
        index + 1
        spec + 1
        var X : math index * space + half_point_width + pad
        var H : math grid * space * 2 + pad / 2
        var labPos1 : math center + 10
        var labPos2 : math X + 2
        instance shape name:lineH'index', y:pad, x:X, w:width, h:H, bg:[255;255;255]
        instance label name:labH'index', bg:[0;0;0], fc:[255;255;255], t:'spec', y:labPos1, x:labPos2
        bringto front labH'index'
        Graph add : lineH'index', labH'index'
    }
    // Vertical
    var index 0
    var spec : math grid
    loop (grid * 2 - 1) {
        index + 1
        spec - 1
        var Y : math index * space + half_point_width + pad
        var W : math grid * space * 2 + pad / 2
        var labPos1 : math center + 10
        var labPos2 : math Y + 2
        instance shape name:lineV'index', x:pad, y:Y, h:width, w:W, bg:[255;255;255]
        instance label name:labV'index', bg:[0;0;0], fc:[255;255;255], t:'spec', x:labPos1, y:labPos2
        bringto front labV'index'
        Graph add : lineV'index', labV'index'
    }
endmethod